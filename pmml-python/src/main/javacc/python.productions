private Token String():
{
	Token token;
}
{
	(
		token = <STRING>
		|
		token = <MULTILINE_STRING>
	)

	{
		return token;
	}
}

private Token Sign():
{
	Token token;
}
{
	(
		token = <PLUS> 
		|
		token = <MINUS>
	)

	{
		return token;
	}
}

private int ColumnIndex():
{
	Token sign = null;
	Token column;
}
{
	(sign = Sign())? column = <INT>

	{
		return PythonParserUtil.parseInt(sign, column);
	}
}

private int ArrayColumnIndex():
{
	int columnIndex;
}
{
	(
		<COLON> <COMMA> (<LBRACKET> columnIndex = ColumnIndex() <RBRACKET> | columnIndex = ColumnIndex())
		|
		columnIndex = ColumnIndex()
	)

	{
		return columnIndex;
	}
}

private String ColumnName():
{
	Token column;
}
{
	column = String()

	{
		return PythonParserUtil.parseString(column);
	}
}

private String DottedName():
{
	Token name;
	StringBuilder sb = new StringBuilder();
}
{
	name = <NAME> { sb.append(name.image); } ( LOOKAHEAD(<DOT> <NAME>) <DOT> name = <NAME> { sb.append('.').append(name.image); } )*

	{
		return sb.toString();
	}
}

private String CanonicalizedDottedName():
{
	String dottedName;
}
{
	dottedName = DottedName()

	{
		return canonicalizeDottedName(dottedName);
	}
}