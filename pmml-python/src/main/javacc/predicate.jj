/*
 * Copyright (c) 2018 Villu Ruusmann
 *
 * This file is part of JPMML-Python
 *
 * JPMML-Python is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JPMML-Python is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with JPMML-Python.  If not, see <http://www.gnu.org/licenses/>.
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "11";
	STATIC = false;
	UNICODE_INPUT = true;
	BUILD_TOKEN_MANAGER = false;
}

PARSER_BEGIN(PredicateTranslator)
package org.jpmml.python;

import java.util.ArrayList;
import java.util.List;

import org.dmg.pmml.Apply;
import org.dmg.pmml.Array;
import org.dmg.pmml.CompoundPredicate;
import org.dmg.pmml.DataType;
import org.dmg.pmml.Expression;
import org.dmg.pmml.False;
import org.dmg.pmml.FieldRef;
import org.dmg.pmml.PMMLFunctions;
import org.dmg.pmml.Predicate;
import org.dmg.pmml.SimplePredicate;
import org.dmg.pmml.SimpleSetPredicate;
import org.dmg.pmml.True;
import org.jpmml.converter.Feature;
import org.jpmml.converter.FieldNameUtil;
import org.jpmml.converter.OperationException;
import org.jpmml.converter.PMMLUtil;
import org.jpmml.converter.TypeUtil;
import org.jpmml.converter.ValueUtil;

public class PredicateTranslator extends AbstractTranslator {

	public PredicateTranslator(Scope scope){
		this(new NullProvider());

		setScope(scope);
	}

	@Override
	public Expression encodeFunction(String dottedName, List<?> arguments){
		Expression expression = super.encodeFunction(dottedName, arguments);

		if(expression instanceof Apply){
			Apply apply = (Apply)expression;

			String name = formatApply(apply);

			ensureDerivedField(name, null, apply);
		}

		return expression;
	}

	public Predicate translatePredicate(String string){
		ReInit(new StringProvider(string));

		Predicate predicate;

		try {
			predicate = translatePredicateInternal();
		} catch(Exception e){
			throw new PredicateTranslationException(string, e);
		}

		return predicate;
	}

	static
	private Object ensureOperand(Object object){
		
		if(object == null){
			throw new OperationException("Operand is not defined");
		}
		
		return object;
	}
	
	static
	private SimplePredicate.Operator translateRelationalOperator(Token operator) throws ParseException {

		switch(operator.kind){
			case EQUAL:
				return SimplePredicate.Operator.EQUAL;
			case NOT_EQUAL:
				return SimplePredicate.Operator.NOT_EQUAL;
			case LESS_THAN:
				return SimplePredicate.Operator.LESS_THAN;
			case LESS_OR_EQUAL:
				return SimplePredicate.Operator.LESS_OR_EQUAL;
			case GREATER_THAN:
				return SimplePredicate.Operator.GREATER_THAN;
			case GREATER_OR_EQUAL:
				return SimplePredicate.Operator.GREATER_OR_EQUAL;
			default:
				throw new ParseException();
		}
	}

	static
	private SimplePredicate.Operator flipRelationalOperator(SimplePredicate.Operator operator){

		switch(operator){
			case EQUAL:
			case NOT_EQUAL:
				return operator;
			case LESS_THAN:
				return SimplePredicate.Operator.GREATER_THAN;
			case LESS_OR_EQUAL:
				return SimplePredicate.Operator.GREATER_OR_EQUAL;
			case GREATER_THAN:
				return SimplePredicate.Operator.LESS_THAN;
			case GREATER_OR_EQUAL:
				return SimplePredicate.Operator.LESS_OR_EQUAL;
			default:
				throw new IllegalArgumentException();
		}
	}

	static
	private String getOnlyArgument(Apply apply){
		List<Expression> expressions = apply.getExpressions();

		ClassDictUtil.checkSize(1, expressions);

		FieldRef fieldRef = (FieldRef)expressions.get(0);

		return fieldRef.requireField();
	}

	static
	private Array createArray(List<?> values){
		DataType dataType = TypeUtil.getDataType(values, DataType.STRING);

		return PMMLUtil.createArray(dataType, values);
	}

	static
	private SimplePredicate createSimplePredicate(Object left, SimplePredicate.Operator operator, Object right){
		String name = asFieldName(left);
		Object value = (right != null ? asValue(right) : null);

		return new SimplePredicate(name, operator, value);
	}

	static
	private SimpleSetPredicate createSimpleSetPredicate(Object left, SimpleSetPredicate.BooleanOperator booleanOperator, Object right){
		String name = asFieldName(left);
		Array array = (Array)right;

		return new SimpleSetPredicate(name, booleanOperator, array);
	}

	static
	private CompoundPredicate createCompoundPredicate(Predicate left, CompoundPredicate.BooleanOperator booleanOperator, Predicate right){

		if(left instanceof CompoundPredicate){
			CompoundPredicate compoundPredicate = (CompoundPredicate)left;

			if(compoundPredicate.requireBooleanOperator() == booleanOperator){
				compoundPredicate.addPredicates(right);

				return compoundPredicate;
			}
		}

		CompoundPredicate compoundPredicate = new CompoundPredicate(booleanOperator, null)
			.addPredicates(left, right);

		return compoundPredicate;
	}

	static
	private SimplePredicate createComparisonPredicate(Object left, SimplePredicate.Operator operator, Object right){
	
		if(isFieldName(left) && isValue(right)){
			return createSimplePredicate(left, operator, right);
		} else
			
		if(isValue(left) && isFieldName(right)){
			return createSimplePredicate(right, flipRelationalOperator(operator), left);
		}

		throw new OperationException("Comparison requires a field name side and a value side");
	}

	static
	private boolean isFieldName(Object object){

		try {
			asFieldName(object);

			return true;
		} catch(OperationException oe){
			return false;
		}
	}

	static
	private String asFieldName(Object object){

		if(object instanceof Feature){
			Feature feature = (Feature)object;

			return feature.getName();
		} // End if

		if(object instanceof Apply){
			Apply apply = (Apply)object;

			String name = formatApply(apply);

			return name;
		} else

		if(object instanceof FieldRef){
			FieldRef fieldRef = (FieldRef)object;

			return fieldRef.requireField();
		} // End if

		throw new OperationException("The left-hand side of the predicate (" + object + ") is not a feature reference");
	}

	static
	private boolean isValue(Object object){

		try {
			asValue(object);

			return true;
		} catch(OperationException oe){
			return false;
		}
	}

	static
	private Object asValue(Object object){

		if(object instanceof Boolean){
			return (Boolean)object;
		} else

		if(object instanceof Integer){
			return (Integer)object;
		} else

		if(object instanceof Double){
			return (Double)object;
		} else

		if(object instanceof String){
			return (String)object;
		}

		throw new OperationException("The right-hand side of the predicate (" + object + ") is not a value");
	}

	static
	private Predicate asPredicate(Object object){

		if(object instanceof Predicate){
			Predicate predicate = (Predicate)object;

			return predicate;
		} // End if

		if(object instanceof Apply){
			Apply apply = (Apply)object;

			switch(apply.requireFunction()){
				case PMMLFunctions.ISMISSING:
					return new SimplePredicate(getOnlyArgument(apply), SimplePredicate.Operator.IS_MISSING, null);
				case PMMLFunctions.ISNOTMISSING:
					return new SimplePredicate(getOnlyArgument(apply), SimplePredicate.Operator.IS_NOT_MISSING, null);
				default:
					break;
			}
		} // End if

		if(object instanceof Feature){
			Feature feature = (Feature)object;

			if(feature.getDataType() == DataType.BOOLEAN){
				return new SimplePredicate(feature.getName(), SimplePredicate.Operator.EQUAL, Boolean.TRUE);
			}
		} // End if

		if((Boolean.FALSE).equals(object)){
			return False.INSTANCE;
		} else
			
		if((Boolean.TRUE).equals(object)){
			return True.INSTANCE;
		} // End if

		throw new OperationException("The expression (" + object + ") is not a predicate");
	}

	static
	private Object asArgument(Object object){

		if(object instanceof Feature){
			Feature feature = (Feature)object;
			
			return feature;
		}

		throw new OperationException("The function argument (" + object + ") is not a feature reference");
	}

	static
	private String formatApply(Apply apply){
		String function = apply.requireFunction();
		List<String> arguments = new ArrayList<>();

		if(apply.hasExpressions()){
			List<Expression> expressions = apply.getExpressions();
			
			for(int i = 0; i < expressions.size(); i++){
				FieldRef fieldRef = (FieldRef)expressions.get(i);
				
				arguments.add(fieldRef.requireField());
			}
		}

		return FieldNameUtil.create(function, arguments);
	}
}
PARSER_END(PredicateTranslator)

@python.tokens@

@python.productions@

private Predicate translatePredicateInternal():
{
	Predicate predicate;
}
{
	predicate = Predicate() <EOF>

	{
		return predicate;
	}
}

private Predicate Predicate():
{
	Predicate predicate;
}
{
	predicate = LogicalOrExpression()

	{
		return predicate;
	}
}

private Predicate LogicalOrExpression():
{
	Predicate left;
	Predicate right;
}
{
	left = LogicalAndExpression() ( <OR> right = LogicalAndExpression() { left = createCompoundPredicate(left, CompoundPredicate.BooleanOperator.OR, right); } )*

	{
		return left;
	}
}

private Predicate LogicalAndExpression():
{
	Predicate left;
	Predicate right;
}
{
	left = ComparisonExpression() ( <AND> right = ComparisonExpression() { left = createCompoundPredicate(left, CompoundPredicate.BooleanOperator.AND, right); } )*

	{
		return left;
	}
}

private Predicate ComparisonExpression():
{
	Object left;
	Token operator = null;
	Object right;
	Object prevRight;
	List<Predicate> predicates = new ArrayList<>();
}
{
	left = PrimaryExpression() { prevRight = left; }
	(
		(
			LOOKAHEAD(<IS> <NONE>)
			<IS> <NONE> { left = createSimplePredicate(ensureOperand(prevRight), SimplePredicate.Operator.IS_MISSING, null); right = null; }
			|
			<IS> <NOT> <NONE> { left = createSimplePredicate(ensureOperand(prevRight), SimplePredicate.Operator.IS_NOT_MISSING, null); right = null; }
			|
			<IN> right = ListMakerExpression() { left = createSimpleSetPredicate(ensureOperand(prevRight), SimpleSetPredicate.BooleanOperator.IS_IN, right); right = null; }
			|
			<NOT> <IN> right = ListMakerExpression() { left = createSimpleSetPredicate(ensureOperand(prevRight), SimpleSetPredicate.BooleanOperator.IS_NOT_IN, right); right = null; }
			|
			(operator = <EQUAL> | operator = <NOT_EQUAL> | operator = <LESS_THAN> | operator = <LESS_OR_EQUAL> | operator = <GREATER_THAN> | operator = <GREATER_OR_EQUAL>) right = UnaryExpression() { left = createComparisonPredicate(ensureOperand(prevRight), translateRelationalOperator(operator), right); }
		)
		{
			left = asPredicate(left);
			predicates.add((Predicate)left);
			prevRight = right;
		}
	)*

	{
		if(predicates.size() <= 1){
			return asPredicate(left);
		} else

		{
			return new CompoundPredicate(CompoundPredicate.BooleanOperator.AND, (List)predicates);
		}
	}
}

private Object UnaryExpression():
{
	Token sign = null;
	Object result;
}
{
	(
		sign = Sign() result = UnaryExpression()
		|
		result = PrimaryExpression()
	)

	{
		if(sign != null && sign.kind == MINUS){
			result = ValueUtil.toNegative(asValue(result));
		}

		return result;
	}
}

private Object PrimaryExpression():
{
	String dottedName;
	Object result;
}
{
	(
		dottedName = CanonicalizedDottedName()
		(
			result = ArrayIndexingExpression(dottedName)
			|
			result = FunctionInvocationExpression(dottedName)
			|
			result = NameInvocationExpression(dottedName)
		)
		|
		result = ParenthesizedExpression()
		|
		result = LiteralExpression()
	)

	{
		return result;
	}
}

private Feature ArrayIndexingExpression(String dottedName):
{
	Scope scope = ensureScope();

	int colIndex;
	String colName;

	Feature feature;
}
{
	(
		LOOKAHEAD(<LBRACKET> ArrayColumnIndex())
		<LBRACKET> colIndex = ArrayColumnIndex() <RBRACKET> {
			feature = scope.getFeature(dottedName, colIndex);
		}
		|
		<LBRACKET> colName = ColumnName() <RBRACKET> {
			feature = scope.getFeature(dottedName, colName);
		}
	)

	{
		return feature;
	}
}

private Object FunctionInvocationExpression(String dottedName):
{
	List<Object> arguments;
}
{
	arguments = Arguments()

	{
		return encodeFunction(dottedName, arguments);
	}
}

private List<Object> Arguments():
{
	Object argument;
	List<Object> arguments = new ArrayList<>();
}
{
	(
		LOOKAHEAD(<LPAREN> <RPAREN>)
		<LPAREN> <RPAREN>
		|
		<LPAREN> argument = PrimaryExpression() { arguments.add(asArgument(argument)); } ( <COMMA> argument = PrimaryExpression() { arguments.add(asArgument(argument)); } )* <RPAREN>
	)

	{
		return arguments;
	}
}

private Object NameInvocationExpression(String dottedName):
{
	Scope scope = ensureScope();
}
{
	{
		Feature feature = scope.getFeature(dottedName);

		return feature;
	}
}

private Predicate ParenthesizedExpression():
{
	Predicate predicate;
}
{
	<LPAREN> predicate = Predicate() <RPAREN>

	{
		return predicate;
	}
}

private Object LiteralExpression():
{
	Token value;
}
{
	(
		value = <FALSE>
		|
		value = <TRUE>
		|
		value = <INT>
		|
		value = <FLOAT>
		|
		value = String()
	)

	{
		return PythonParserUtil.parseValue(value);
	}
}

private Array ListMakerExpression():
{
	Object predicate;
	
	List<Object> values = new ArrayList<Object>();
}
{
	<LBRACKET> predicate = UnaryExpression() { values.add(asValue(predicate)); } ( <COMMA> predicate = UnaryExpression() { values.add(asValue(predicate)); } )* <RBRACKET>
	
	{
		return createArray(values);
	}
}
