/*
 * Copyright (c) 2017 Villu Ruusmann
 *
 * This file is part of JPMML-Python
 *
 * JPMML-Python is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JPMML-Python is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with JPMML-Python.  If not, see <http://www.gnu.org/licenses/>.
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "1.8";
	STATIC = false;
	BUILD_TOKEN_MANAGER = false;
}

PARSER_BEGIN(ExpressionTranslator)
package org.jpmml.python;

import java.util.ArrayList;
import java.util.List;

import com.google.common.collect.Iterables;
import org.dmg.pmml.Apply;
import org.dmg.pmml.Constant;
import org.dmg.pmml.DataType;
import org.dmg.pmml.DerivedField;
import org.dmg.pmml.Expression;
import org.dmg.pmml.FieldRef;
import org.dmg.pmml.OpType;
import org.dmg.pmml.PMMLFunctions;
import org.jpmml.converter.Feature;
import org.jpmml.converter.PMMLEncoder;
import org.jpmml.converter.PMMLUtil;
import org.jpmml.converter.TypeUtil;
import org.jpmml.converter.visitors.ExpressionCompactor;

public class ExpressionTranslator extends AbstractTranslator {

	public ExpressionTranslator(Scope scope){
		this(new NullProvider());

		setScope(scope);
	}

	public DerivedField translateDef(String string){
		ReInit(new StringProvider(ensureTerminator(string)));

		DerivedField derivedField;

		try {
			derivedField = translateDefInternal();
		} catch(ParseException pe){
			throw new IllegalArgumentException("Python function definition \'" + toSingleLine(string) + "\' is either invalid or not supported", pe);
		}

		return derivedField;
	}

	public Expression translateStatement(String string){
		return translateStatement(string, true);
	}

	public Expression translateStatement(String string, boolean compact){
		ReInit(new StringProvider(ensureTerminator(string)));

		Expression statement;

		try {
			statement = translateStatementInternal();
		} catch(ParseException pe){
			throw new IllegalArgumentException("Python statement \'" + toSingleLine(string) + "\' is either invalid or not supported", pe);
		}

		if(compact){
			statement = compact(statement);
		}

		return statement;
	}

	public Expression translateExpression(String string){
		return translateExpression(string, true);
	}

	public Expression translateExpression(String string, boolean compact){
		ReInit(new StringProvider(string));

		Expression expression;

		try {
			expression = translateExpressionInternal();
		} catch(ParseException pe){
			throw new IllegalArgumentException("Python expression \'" + toSingleLine(string) + "\' is either invalid or not supported", pe);
		}

		if(compact){
			expression = compact(expression);
		}

		return expression;
	}

	private PMMLEncoder ensureEncoder(){
		Scope scope = ensureScope();

		PMMLEncoder encoder = scope.getEncoder();
		if(encoder == null){
			throw new IllegalStateException();
		}

		return encoder;
	}

	private DerivedField createDerivedField(String name, Expression expression){
		PMMLEncoder encoder = ensureEncoder();

		OpType opType = null;
		DataType dataType = TypeUtil.getDataType(expression, this);

		if(dataType != null){
			opType = TypeUtil.getOpType(dataType);
		} else

		// XXX
		{
			opType = OpType.CONTINUOUS;
			dataType = DataType.DOUBLE;
		}

		return encoder.createDerivedField(name, opType, dataType, expression);
	}

	static
	public Expression compact(Expression expression){
		ExpressionCompactor expressionCompactor = new ExpressionCompactor();

		expressionCompactor.applyTo(expression);

		return expression;
	}

	static
	private String translateRelationalOperator(Token operator) throws ParseException {

		switch(operator.kind){
			case EQUAL:
				return PMMLFunctions.EQUAL;
			case NOT_EQUAL:
				return PMMLFunctions.NOTEQUAL;
			case LESS_THAN:
				return PMMLFunctions.LESSTHAN;
			case LESS_OR_EQUAL:
				return PMMLFunctions.LESSOREQUAL;
			case GREATER_THAN:
				return PMMLFunctions.GREATERTHAN;
			case GREATER_OR_EQUAL:
				return PMMLFunctions.GREATEROREQUAL;
			default:
				throw new ParseException();
		}
	}

	static
	private String translateArithmeticOperator(Token operator){

		switch(operator.kind){
			case MODULO:
				return PMMLFunctions.MODULO;
			default:
				return operator.image;
		}
	}

	static
	private Constant translateConstant(String module, Token value){
		module = FunctionUtil.canonicalizeModule(module);

		if((module).equals("numpy")){
			
			switch(value.kind){
				case NAN:
					return PMMLUtil.createMissingConstant();
				default:
					break;
			}
		} else

		if((module).equals("pandas")){

			switch(value.kind){
				case NA:
				case NAT:
					return PMMLUtil.createMissingConstant();
				default:
					break;
			}
		}

		throw new IllegalArgumentException("Constant \'" + (module + "." + value.image) + "\' is not supported");
	}

	static
	private Apply translateTrailerFunction(Expression expression, String function, List<Expression> arguments){

		if(("startswith").equals(function) || ("endswith").equals(function)){
			ClassDictUtil.checkSize(1, arguments);

			Expression argument = arguments.get(0);
			if(!(argument instanceof Constant)){
				throw new IllegalArgumentException();
			}

			Constant constant = (Constant)argument;
			if(constant.getDataType() != DataType.STRING){
				throw new IllegalArgumentException();
			}

			String value = (String)constant.getValue();

			switch(function){
				case "startswith":
					return PMMLUtil.createApply(PMMLFunctions.EQUAL,
						translateStringSlice(expression, 0, value.length()),
						constant
					);
				case "endswith":
					return PMMLUtil.createApply(PMMLFunctions.EQUAL,
						translateStringSlice(expression, -value.length(), null),
						constant
					);
				default:
					break;
			}
		} else

		if(("lower").equals(function) || ("upper").equals(function)){
			ClassDictUtil.checkSize(0, arguments);

			switch(function){
				case "lower":
					return PMMLUtil.createApply(PMMLFunctions.LOWERCASE, expression);
				case "upper":
					return PMMLUtil.createApply(PMMLFunctions.UPPERCASE, expression);
				default:
					break;
			}
		} else

		if(("strip").equals(function)){
			ClassDictUtil.checkSize(0, arguments);

			switch(function){
				case "strip":
					return PMMLUtil.createApply(PMMLFunctions.TRIMBLANKS, expression);
				default:
					break;
			}
		}

		throw new IllegalArgumentException("Function \'" + function + "\' is not supported");
	}

	static
	private Apply translateFunction(String function, List<Expression> arguments){
		String module;
		String name;

		int dot = function.lastIndexOf('.');
		if(dot > -1){
			module = function.substring(0, dot);
			name = function.substring(dot + 1);
		} else

		{
			module = "";
			name = function;
		}

		module = FunctionUtil.canonicalizeModule(module);

		return FunctionUtil.encodeFunction(module, name, arguments);
	}

	static
	private Expression translateStringSlice(Expression expression, Integer startPos, Integer stopPos){

		if(startPos == null && stopPos == null){
			return expression;
		}

		Expression offsetExpression;

		if(startPos != null && startPos >= 0){
			offsetExpression = PMMLUtil.createConstant(startPos + 1, DataType.INTEGER);
		} else

		if(startPos != null && startPos < 0){
			offsetExpression = PMMLUtil.createApply(PMMLFunctions.MAX,
				PMMLUtil.createApply(PMMLFunctions.SUBTRACT, 
					PMMLUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
					PMMLUtil.createConstant((-startPos) - 1, DataType.INTEGER)
				),
				PMMLUtil.createConstant(1, DataType.INTEGER)
			);
		} else

		{
			offsetExpression = PMMLUtil.createConstant(1, DataType.INTEGER);
		} // End if

		Expression lengthExpression;

		if(stopPos != null && stopPos >= 0){

			if(startPos != null && startPos >= 0){
				lengthExpression = PMMLUtil.createConstant(Math.max(stopPos - startPos, 0), DataType.INTEGER);
			} else

			if(startPos != null && startPos < 0){
				lengthExpression = PMMLUtil.createConstant(0, DataType.INTEGER);
			} else

			{
				lengthExpression = PMMLUtil.createConstant(stopPos, DataType.INTEGER);
			}
		} else

		if(stopPos != null && stopPos < 0){

			if(startPos != null && startPos >= 0){
				lengthExpression = PMMLUtil.createApply(PMMLFunctions.MAX,
					PMMLUtil.createApply(PMMLFunctions.SUBTRACT,
						PMMLUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
						PMMLUtil.createConstant(startPos + (-stopPos))
					),
					PMMLUtil.createConstant(0, DataType.INTEGER)
				);
			} else

			if(startPos != null && startPos < 0){
				lengthExpression = PMMLUtil.createConstant(Math.max(stopPos - startPos, 0), DataType.INTEGER);
			} else

			{
				lengthExpression = PMMLUtil.createApply(PMMLFunctions.MAX,
					PMMLUtil.createApply(PMMLFunctions.SUBTRACT,
						PMMLUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
						PMMLUtil.createConstant(-stopPos)
					),
					PMMLUtil.createConstant(0, DataType.INTEGER)
				);
			}
		} else

		{
			lengthExpression = PMMLUtil.createConstant(Integer.MAX_VALUE, DataType.INTEGER);
		}

		return PMMLUtil.createApply(PMMLFunctions.SUBSTRING, expression, offsetExpression, lengthExpression);
	}

	static
	private Object parseValue(Token value) throws ParseException {

		switch(value.kind){
			case NONE:
				return null;
			case FALSE:
			case TRUE:
				return PythonParserUtil.parseBoolean(value);
			case INT:
				return PythonParserUtil.parseInt(value);
			case FLOAT:
				return PythonParserUtil.parseFloat(value);
			case STRING:
				return PythonParserUtil.parseString(value);
			default:
				throw new ParseException();
		}
	}

	static
	private String ensureTerminator(String string){

		if(string.endsWith("\n")){

			if(!string.endsWith("\n\n")){
				string += "\n";
			}
		}

		return string;
	}

	static
	private Expression getOnlyStatement(List<Expression> statements){

		for(int i = 0; i < statements.size(); i++){
			Expression statement = statements.get(i);

			if(statement != null){
				statements = statements.subList(i, statements.size());

				break;
			}
		}

		return Iterables.getOnlyElement(statements);
	}

	static
	private class Block extends ArrayList<Expression> {

		private Block(List<Expression> statements){
			super(statements);
		}
	}
}
PARSER_END(ExpressionTranslator)

@python.tokens@

private DerivedField translateDefInternal():
{
	DerivedField derivedField;
}
{
	derivedField = Def() ( <NEWLINE> )* <EOF>

	{
		return derivedField;
	}
}

private Expression translateStatementInternal():
{
	Expression expression;
}
{
	expression = Statement() ( <NEWLINE> )* <EOF>

	{
		return expression;
	}
}

private Expression translateExpressionInternal():
{
	Expression expression;
}
{
	expression = Expression() <EOF>

	{
		return expression;
	}
}

private DerivedField Def():
{
	Token name;
	List<Token> parameters;
	Block block;
}
{
	<DEF> name = <NAME> parameters = Parameters() <COLON> block = Block()

	{
		return createDerivedField(name.image, getOnlyStatement(block));
	}
}

private List<Token> Parameters():
{
	Token name;
	List<Token> names = new ArrayList();
}
{
	(
		LOOKAHEAD(<LPAREN> <RPAREN>)
		<LPAREN> <RPAREN>
		|
		<LPAREN> name = <NAME> { names.add(name); } ( <COMMA> name = <NAME> { names.add(name); } )* <RPAREN>
	)

	{
		return names;
	}
}

private Block Block():
{
	List<Expression> statements;
}
{
	(
		<NEWLINE> Indent() statements = Statements() Dedent()
		|
		statements = SimpleStatements()
	)
	
	{
		return new Block(statements);
	}
}

JAVACODE
private void Indent(){
}

JAVACODE
private void Dedent(){
}

private List<Expression> Statements():
{
	Expression statement;
	List<Expression> statements = new ArrayList<>();
}
{
	( statement = Statement() { statements.add(statement); } )+

	{
		return statements;
	}
}

private Expression Statement():
{
	Expression statement;
	List<Expression> statements;
}
{
	(
		statement = CompoundStatement() <NEWLINE>
		|
		statements = SimpleStatements() { statement = getOnlyStatement(statements); }
	)

	{
		return statement;
	}
}

private Expression CompoundStatement():
{
	Expression statement;
}
{
	statement = IfStatement()

	{
		return statement;
	}
}

private List<Expression> SimpleStatements():
{
	Expression statement;
	List<Expression> statements = new ArrayList<>();
}
{
	statement = SimpleStatement() { statements.add(statement); } ( <SEMICOLON> statement = SimpleStatement() { statements.add(statement); } )* <NEWLINE>
	
	{
		return statements;
	}
}

private Expression SimpleStatement():
{
	Expression expression;
}
{
	(
		expression = Assignment()
		|
		expression = ReturnStatement()
	)
	
	{
		return expression;
	}
}

private Apply IfStatement():
{
	Expression condition;
	Block block;

	Apply if_statement;
	Apply elif_statement;

	Apply tail;
}
{
	<IF> condition = Expression() <COLON> block = Block() { if_statement = PMMLUtil.createApply(PMMLFunctions.IF, condition, getOnlyStatement(block)); tail = if_statement; } ( elif_statement = ElIfStatement() { tail.addExpressions(elif_statement); tail = elif_statement; } )* ( block = ElseBlock() { tail.addExpressions(getOnlyStatement(block)); } )?

	{
		return if_statement;
	}
}

private Apply ElIfStatement():
{
	Expression condition;
	Block block;
}
{
	<ELIF> condition = Expression() <COLON> block = Block()

	{
		return PMMLUtil.createApply(PMMLFunctions.IF, condition, getOnlyStatement(block));
	}
}

private Block ElseBlock():
{
	Block block;
}
{
	<ELSE> <COLON> block = Block()

	{
		return block;
	}
}

private Expression Assignment():
{
	Token name;
	Expression expression;
}
{
	name = <NAME> <ASSIGN> expression = Expression()
	
	{
		DerivedField derivedField = createDerivedField(name.image, expression);
		
		return null;
	}
}

private Expression ReturnStatement():
{
	Expression expression;
}
{
	<RETURN> expression = Expression()
	
	{
		return expression;
	}
}

private Expression Expression():
{
	Expression expression;
}
{
	expression = IfElseExpression()

	{
		return expression;
	}
}

private Expression IfElseExpression():
{
	Expression left;
	Expression center;
	Expression right;
}
{
	left = LogicalOrExpression() ( <IF> center = Expression() <ELSE> right = Expression() { left = PMMLUtil.createApply(PMMLFunctions.IF, center, left, right); } )?

	{
		return left;
	}
}

private Expression LogicalOrExpression():
{
	Expression left;
	Expression right;
}
{
	left = LogicalAndExpression() ( <OR> right = LogicalAndExpression() { left = PMMLUtil.createApply(PMMLFunctions.OR, left, right); } )*

	{
		return left;
	}
}

private Expression LogicalAndExpression():
{
	Expression left;
	Expression right;
}
{
	left = NegationExpression() ( <AND> right = NegationExpression() { left = PMMLUtil.createApply(PMMLFunctions.AND, left, right); } )*

	{
		return left;
	}
}

private Expression NegationExpression():
{
	Token operator = null;
	Expression expression;
}
{
	( operator = <NOT> )? expression = ComparisonExpression()

	{
		if(operator != null){
			return PMMLUtil.createApply(PMMLFunctions.NOT, expression);
		}

		return expression;
	}
}

private Expression ComparisonExpression():
{
	Expression left;
	Token operator;
	Object right;
}
{
	(
		LOOKAHEAD(AdditiveExpression() <IS> <NONE>)
		left = AdditiveExpression() <IS> <NONE> { left = PMMLUtil.createApply(PMMLFunctions.ISMISSING, left); }
		|
		LOOKAHEAD(AdditiveExpression() <IS> <NOT> <NONE>)
		left = AdditiveExpression() <IS> <NOT> <NONE> { left = PMMLUtil.createApply(PMMLFunctions.ISNOTMISSING, left); }
		|
		LOOKAHEAD(AdditiveExpression() <IN>)
		left = AdditiveExpression() <IN> right = ListMakerExpression() { left = PMMLUtil.createApply(PMMLFunctions.ISIN, left).addExpressions((Expression[])right); }
		|
		LOOKAHEAD(AdditiveExpression() <NOT> <IN>)
		left = AdditiveExpression() <NOT> <IN> right = ListMakerExpression() { left = PMMLUtil.createApply(PMMLFunctions.ISNOTIN, left).addExpressions((Expression[])right); }
		|
		left = AdditiveExpression() ((operator = <EQUAL> | operator = <NOT_EQUAL> | operator = <LESS_THAN> | operator = <LESS_OR_EQUAL> | operator = <GREATER_THAN> | operator = <GREATER_OR_EQUAL>) right = AdditiveExpression() { left = PMMLUtil.createApply(translateRelationalOperator(operator), left, (Expression)right); })?
	)

	{
		return left;
	}
}

private Expression AdditiveExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = MultiplicativeExpression() ((operator = <PLUS> | operator = <MINUS>) right = MultiplicativeExpression() {

		if((operator.kind == PLUS) && (TypeUtil.isString(left, this) && TypeUtil.isString(right, this))){
			left = PMMLUtil.createApply(PMMLFunctions.CONCAT, left, right);
		} else

		{
			left = PMMLUtil.createApply(translateArithmeticOperator(operator), left, right);
		}
	})*

	{
		return left;
	}
}

private Expression MultiplicativeExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = UnaryExpression() ((operator = <MULTIPLY> | operator = <DIVIDE> | operator = <MODULO> ) right = UnaryExpression() { left = PMMLUtil.createApply(translateArithmeticOperator(operator), left, right); })*

	{
		return left;
	}
}

private Expression UnaryExpression():
{
	Token sign = null;
	Expression expression;
}
{
	(
		sign = Sign() expression = UnaryExpression()
		|
		expression = PrimaryExpression()
	)

	{
		if(sign != null && sign.kind == MINUS){
			expression = PMMLUtil.toNegative(expression);
		}

		return expression;
	}
}

private Expression PrimaryExpression():
{
	String featureOrFunction;
	Expression expression;
}
{
	(
		featureOrFunction = DottedName()
		(
			LOOKAHEAD(ArrayIndexingExpression())
			expression = ArrayIndexingExpression(featureOrFunction) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
			|
			expression = FunctionInvocationExpression(featureOrFunction) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
			|
			expression = ConstantExpression(featureOrFunction)
			|
			expression = NameInvocationExpression(featureOrFunction) ( expression = StringSlicingExpression(expression) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )* )?
		)
		|
		expression = ParenthesizedExpression() ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
		|
		expression = LiteralExpression()
	)

	{
		return expression;
	}
}

private String DottedName():
{
	Token name;
	StringBuilder sb = new StringBuilder();
}
{
	name = <NAME> { sb.append(name.image); } ( LOOKAHEAD(<DOT> <NAME>) <DOT> name = <NAME> { sb.append('.').append(name.image); })*

	{
		return sb.toString();
	}
}

private FieldRef ArrayIndexingExpression(String dottedName):
{
	Token sign = null;
	Token column;

	Feature feature;
}
{
	(
		LOOKAHEAD(<LBRACKET> (Sign())? <INT>)
		<LBRACKET> (sign = Sign())? column = <INT> <RBRACKET> {
			Scope scope = ensureScope();
			
			int colIndex = PythonParserUtil.parseInt(sign, column);
			
			feature = scope.getFeature(dottedName, colIndex);
		}
		|
		<LBRACKET> column = <STRING> <RBRACKET> {
			Scope scope = ensureScope();
			
			String colName = PythonParserUtil.parseString(column);
			
			feature = scope.getFeature(dottedName, colName);
		}
	)

	{
		return feature.ref();
	}
}

private Apply FunctionInvocationExpression(String dottedName):
{
	List<Expression> arguments;
}
{
	arguments = Arguments()

	{
		Scope scope = ensureScope();

		int dot = dottedName.lastIndexOf('.');
		if(dot > -1){
			String featureName = dottedName.substring(0, dot);
			String functionName = dottedName.substring(dot + 1);

			Feature feature = scope.resolveFeature(featureName);
			if(feature != null){
				return translateTrailerFunction(feature.ref(), functionName, arguments);
			}
		}

		return translateFunction(dottedName, arguments);
	}
}

private Apply TrailerFunctionInvocationExpression(Expression expression):
{
	Token name;
	List<Expression> arguments;
}
{
	<DOT> name = <NAME> arguments = Arguments()

	{
		return translateTrailerFunction(expression, name.image, arguments);
	}
}

private List<Expression> Arguments():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	(
		LOOKAHEAD(<LPAREN> <RPAREN>)
		<LPAREN> <RPAREN>
		|
		<LPAREN> expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); })* <RPAREN>
	)

	{
		return expressions;
	}
}

private Expression StringSlicingExpression(Expression expression):
{
	Token startSign = null;
	Token start = null;
	Token stopSign = null;
	Token stop = null;
}
{
	<LBRACKET> ((startSign = Sign())? start = <INT>)? <COLON> ((stopSign = Sign())? stop = <INT>)? <RBRACKET>

	{
		Integer startPos = null;
		if(start != null){
			startPos = PythonParserUtil.parseInt(startSign, start);
		}

		Integer stopPos = null;
		if(stop != null){
			stopPos = PythonParserUtil.parseInt(stopSign, stop);
		}

		return translateStringSlice(expression, startPos, stopPos);
	}
}

private Constant ConstantExpression(String dottedName):
{
	Token value;
}
{
	<DOT>
	(
		value = <NA>
		|
		value = <NAN>
		|
		value = <NAT>
	)

	{
		return translateConstant(dottedName, value);
	}
}

private FieldRef NameInvocationExpression(String dottedName):
{
}
{
	{
		Scope scope = ensureScope();

		Feature feature = scope.getFeature(dottedName);

		return feature.ref();
	}
}

private Expression ParenthesizedExpression():
{
	Expression expression;
}
{
	<LPAREN> expression = Expression() <RPAREN>

	{
		return expression;
	}
}

private Constant LiteralExpression():
{
	Token value;
	DataType dataType;
	Boolean missing = null;
}
{
	(
		value = <NONE> { dataType = null; missing = Boolean.TRUE; }
		|
		value = <FALSE> { dataType = DataType.BOOLEAN; }
		|
		value = <TRUE> { dataType = DataType.BOOLEAN; }
		|
		value = <INT> { dataType = DataType.INTEGER; }
		|
		value = <FLOAT> { dataType = DataType.DOUBLE; }
		|
		value = <STRING> { dataType = DataType.STRING; }
	)

	{
		Constant constant = new Constant()
			.setValue(parseValue(value))
			.setDataType(dataType)
			.setMissing(missing);

		return constant;
	}
}

private Expression[] ListMakerExpression():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	<LBRACKET> expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); })* <RBRACKET>

	{
		return expressions.toArray(new Expression[expressions.size()]);
	}
}
