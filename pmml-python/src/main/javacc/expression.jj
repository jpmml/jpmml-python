/*
 * Copyright (c) 2017 Villu Ruusmann
 *
 * This file is part of JPMML-Python
 *
 * JPMML-Python is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JPMML-Python is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with JPMML-Python.  If not, see <http://www.gnu.org/licenses/>.
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "11";
	STATIC = false;
	BUILD_TOKEN_MANAGER = false;
}

PARSER_BEGIN(ExpressionTranslator)
package org.jpmml.python;

import java.util.ArrayDeque;
import java.util.ArrayList;
import java.util.Collection;
import java.util.Collections;
import java.util.Deque;
import java.util.Iterator;
import java.util.LinkedHashMap;
import java.util.List;
import java.util.Map;

import org.dmg.pmml.Apply;
import org.dmg.pmml.Constant;
import org.dmg.pmml.DataType;
import org.dmg.pmml.DefineFunction;
import org.dmg.pmml.DerivedField;
import org.dmg.pmml.Expression;
import org.dmg.pmml.FieldRef;
import org.dmg.pmml.ParameterField;
import org.dmg.pmml.PMMLFunctions;
import org.jpmml.converter.ExpressionUtil;
import org.jpmml.converter.Feature;
import org.jpmml.converter.PMMLEncoder;
import org.jpmml.converter.visitors.ExpressionCompactor;

public class ExpressionTranslator extends AbstractTranslator {

	private Deque<String> indents = new ArrayDeque<>();

	private Map<String, DerivedField> variableMap = new LinkedHashMap<>();


	public ExpressionTranslator(Scope scope){
		this(new NullProvider());

		setScope(scope);
	}

	public DefineFunction translateDef(String string){
		ReInit(new StringProvider(ensureTerminator(string)));

		DefineFunction defineFunction;

		try {
			defineFunction = translateDefInternal();
		} catch(ParseException pe){
			throw new TranslationException("Python function definition \'" + toSingleLine(string) + "\' is either invalid or not supported", pe);
		}

		return defineFunction;
	}

	public Expression translateStatement(String string){
		return translateStatement(string, true);
	}

	public Expression translateStatement(String string, boolean compact){
		ReInit(new StringProvider(ensureTerminator(string)));

		Expression statement;

		try {
			statement = translateStatementInternal();
		} catch(ParseException pe){
			throw new TranslationException("Python statement \'" + toSingleLine(string) + "\' is either invalid or not supported", pe);
		}

		if(compact){
			statement = compact(statement);
		}

		return statement;
	}

	public Expression translateExpression(String string){
		return translateExpression(string, true);
	}

	public Expression translateExpression(String string, boolean compact){
		ReInit(new StringProvider(string));

		Expression expression;

		try {
			expression = translateExpressionInternal();
		} catch(ParseException pe){
			throw new TranslationException("Python expression \'" + toSingleLine(string) + "\' is either invalid or not supported", pe);
		}

		if(compact){
			expression = compact(expression);
		}

		return expression;
	}

	public Constant encodeConstant(String dottedName){

		switch(dottedName){
			case "math.e":
			case "numpy.e":
				return ExpressionUtil.createConstant(DataType.DOUBLE, Math.E);
			case "numpy.NZERO":
				return ExpressionUtil.createConstant(DataType.DOUBLE, -0.0d);
			case "math.pi":
			case "numpy.pi":
				return ExpressionUtil.createConstant(DataType.DOUBLE, Math.PI);
			case "numpy.PZERO":
				return ExpressionUtil.createConstant(DataType.DOUBLE, 0.0);
			case "math.tau":
				return ExpressionUtil.createConstant(DataType.DOUBLE, 2 * Math.PI);
			default:
				break;
		}

		switch(dottedName){
			// Math
			case "math.nan":
			// Numpy
			case "numpy.nan":
			case "numpy.NaN":
			case "numpy.NAN":
			// Pandas
			case "pandas.NA":
			case "pandas.NaT":
				return ExpressionUtil.createMissingConstant();
			default:
				break;
		}

		throw new TranslationException("Constant \'" + dottedName + "\' is not supported");
	}

	private String getIndent() throws ParseException {
		Token nextToken = getToken(1);
		
		return PythonParserUtil.getWhitespace(nextToken);
	}

	private void checkIndent(String indent) throws ParseException {
		String expectedIndent = this.indents.peek();

		if(!(indent).equals(expectedIndent)){
			throw new ParseException("Unexpected indent");
		}
	}

	private FieldRef getVariable(String name){
		DerivedField derivedField = this.variableMap.get(name);

		if(derivedField != null){
			return new FieldRef(derivedField);
		}

		return null;
	}

	private void putVariable(String name, Expression expression){
		PMMLEncoder encoder = ensureEncoder();

		if(this.variableMap.containsKey(name)){
			throw new TranslationException("Python variable \'" + name + "\' re-assigned in function body");
		}

		DerivedField derivedField = ensureDerivedField(name, expression);

		// XXX
		encoder.removeDerivedField(derivedField.requireName());

		this.variableMap.put(name, derivedField);
	}

	static
	public Expression compact(Expression expression){
		ExpressionCompactor expressionCompactor = new ExpressionCompactor();

		expressionCompactor.applyTo(expression);

		return expression;
	}

	static
	private String translateRelationalOperator(Token operator) throws ParseException {

		switch(operator.kind){
			case EQUAL:
				return PMMLFunctions.EQUAL;
			case NOT_EQUAL:
				return PMMLFunctions.NOTEQUAL;
			case LESS_THAN:
				return PMMLFunctions.LESSTHAN;
			case LESS_OR_EQUAL:
				return PMMLFunctions.LESSOREQUAL;
			case GREATER_THAN:
				return PMMLFunctions.GREATERTHAN;
			case GREATER_OR_EQUAL:
				return PMMLFunctions.GREATEROREQUAL;
			default:
				throw new ParseException();
		}
	}

	static
	private String translateArithmeticOperator(Token operator){

		switch(operator.kind){
			case MODULO:
				return PMMLFunctions.MODULO;
			default:
				return operator.image;
		}
	}

	static
	private Apply translateTrailerFunction(Expression expression, String name, List<Expression> arguments){

		if(("startswith").equals(name) || ("endswith").equals(name)){
			ClassDictUtil.checkSize(1, arguments);

			Expression argument = arguments.get(0);
			if(!(argument instanceof Constant)){
				throw new IllegalArgumentException();
			}

			Constant constant = (Constant)argument;
			if(constant.getDataType() != DataType.STRING){
				throw new IllegalArgumentException();
			}

			String value = (String)constant.getValue();

			switch(name){
				case "startswith":
					return ExpressionUtil.createApply(PMMLFunctions.EQUAL,
						translateStringSlice(expression, 0, value.length()),
						constant
					);
				case "endswith":
					return ExpressionUtil.createApply(PMMLFunctions.EQUAL,
						translateStringSlice(expression, -value.length(), null),
						constant
					);
				default:
					break;
			}
		} else

		if(("lower").equals(name) || ("upper").equals(name)){
			ClassDictUtil.checkSize(0, arguments);

			switch(name){
				case "lower":
					return ExpressionUtil.createApply(PMMLFunctions.LOWERCASE, expression);
				case "upper":
					return ExpressionUtil.createApply(PMMLFunctions.UPPERCASE, expression);
				default:
					break;
			}
		} else

		if(("strip").equals(name)){
			ClassDictUtil.checkSize(0, arguments);

			switch(name){
				case "strip":
					return ExpressionUtil.createApply(PMMLFunctions.TRIMBLANKS, expression);
				default:
					break;
			}
		}

		throw new TranslationException("Function \'" + name + "\' is not supported");
	}

	static
	private Expression translateStringSlice(Expression expression, Integer startPos, Integer stopPos){

		if(startPos == null && stopPos == null){
			return expression;
		}

		Expression offsetExpression;

		if(startPos != null && startPos >= 0){
			offsetExpression = ExpressionUtil.createConstant(DataType.INTEGER, startPos + 1);
		} else

		if(startPos != null && startPos < 0){
			offsetExpression = ExpressionUtil.createApply(PMMLFunctions.MAX,
				ExpressionUtil.createApply(PMMLFunctions.SUBTRACT, 
					ExpressionUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
					ExpressionUtil.createConstant(DataType.INTEGER, (-startPos) - 1)
				),
				ExpressionUtil.createConstant(DataType.INTEGER, 1)
			);
		} else

		{
			offsetExpression = ExpressionUtil.createConstant(DataType.INTEGER, 1);
		} // End if

		Expression lengthExpression;

		if(stopPos != null && stopPos >= 0){

			if(startPos != null && startPos >= 0){
				lengthExpression = ExpressionUtil.createConstant(DataType.INTEGER, Math.max(stopPos - startPos, 0));
			} else

			if(startPos != null && startPos < 0){
				lengthExpression = ExpressionUtil.createConstant(DataType.INTEGER, 0);
			} else

			{
				lengthExpression = ExpressionUtil.createConstant(DataType.INTEGER, stopPos);
			}
		} else

		if(stopPos != null && stopPos < 0){

			if(startPos != null && startPos >= 0){
				lengthExpression = ExpressionUtil.createApply(PMMLFunctions.MAX,
					ExpressionUtil.createApply(PMMLFunctions.SUBTRACT,
						ExpressionUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
						ExpressionUtil.createConstant(DataType.INTEGER, startPos + (-stopPos))
					),
					ExpressionUtil.createConstant(DataType.INTEGER, 0)
				);
			} else

			if(startPos != null && startPos < 0){
				lengthExpression = ExpressionUtil.createConstant(DataType.INTEGER, Math.max(stopPos - startPos, 0));
			} else

			{
				lengthExpression = ExpressionUtil.createApply(PMMLFunctions.MAX,
					ExpressionUtil.createApply(PMMLFunctions.SUBTRACT,
						ExpressionUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
						ExpressionUtil.createConstant(DataType.INTEGER, -stopPos)
					),
					ExpressionUtil.createConstant(DataType.INTEGER, 0)
				);
			}
		} else

		{
			lengthExpression = ExpressionUtil.createConstant(DataType.INTEGER, Integer.MAX_VALUE);
		}

		return ExpressionUtil.createApply(PMMLFunctions.SUBSTRING, expression, offsetExpression, lengthExpression);
	}

	static
	private String ensureTerminator(String string){

		if(string.endsWith("\n")){

			if(!string.endsWith("\n\n")){
				string += "\n";
			}
		}

		return string;
	}

	static
	private Expression getOnlyStatement(List<Expression> statements){

		for(Iterator<Expression> it = statements.iterator(); it.hasNext(); ){
			Expression statement = it.next();

			if(statement == null){
				it.remove();
			}
		}

		if(statements.size() == 0){
			throw new TranslationException("Block does not contain any PMML-compatible statements");
		} else

		if(statements.size() == 1){
			return statements.get(0);
		} else

		{
			throw new TranslationException("Block contains more than one PMML-compatible statements");
		}
	}

	static
	private class Block extends ArrayList<Expression> {

		private Block(List<Expression> statements){
			super(statements);
		}

		Expression getOnlyStatement(){
			return ExpressionTranslator.getOnlyStatement(this);
		}
	}
}
PARSER_END(ExpressionTranslator)

@python.tokens@

@python.productions@

private DefineFunction translateDefInternal():
{
	DefineFunction defineFunction;
}
{
	defineFunction = Def() ( <NEWLINE> )* <EOF>

	{
		return defineFunction;
	}
}

private Expression translateStatementInternal():
{
	List<Expression> expressions;
}
{
	expressions = Statement() ( <NEWLINE> )* <EOF>

	{
		return getOnlyStatement(expressions);
	}
}

private Expression translateExpressionInternal():
{
	Expression expression;
}
{
	expression = Expression() <EOF>

	{
		return expression;
	}
}

private DefineFunction Def():
{
	Token name;
	List<Token> parameters;
	Block block;
}
{
	{
		this.variableMap.clear();
	}

	<DEF> name = <NAME> parameters = Parameters() <COLON> block = Block()

	{
		DefineFunction defineFunction = createDefineFunction(name.image, block.getOnlyStatement());

		for(int i = 0; i < parameters.size(); i++){
			Token parameter = parameters.get(i);

			defineFunction.addParameterFields(new ParameterField(parameter.image));
		}

		Collection<DerivedField> derivedFields = this.variableMap.values();
		for(DerivedField derivedField : derivedFields){
			defineFunction.addDerivedFields(derivedField);
		}

		this.variableMap.clear();

		return defineFunction;
	}
}

private List<Token> Parameters():
{
	Token name;
	List<Token> names = new ArrayList();
}
{
	(
		LOOKAHEAD(<LPAREN> <RPAREN>)
		<LPAREN> <RPAREN>
		|
		<LPAREN> name = <NAME> { names.add(name); } ( <COMMA> name = <NAME> { names.add(name); } )* <RPAREN>
	)

	{
		return names;
	}
}

private Block Block():
{
	List<Expression> statements;
}
{
	(
		(<NEWLINE>)+ Indent() statements = Statements() Dedent()
		|
		statements = SimpleStatements()
	)
	
	{
		return new Block(statements);
	}
}

JAVACODE
private void Indent(){
	String indent = getIndent();

	if(!this.indents.isEmpty()){
		String prevIndent = this.indents.peek();

		if(!(indent).startsWith(prevIndent)){
			throw new ParseException();
		}
	}

	this.indents.push(indent);
}

JAVACODE
private void Dedent(){
	this.indents.pop();
}

JAVACODE
private boolean BlockContinuation(){
	String expectedIndent = this.indents.peek();

	String indent = getIndent();

	if((indent).equals(expectedIndent)){
		return true;
	}

	return false;
}

private List<Expression> Statements():
{
	List<Expression> statement;
	List<Expression> statements = new ArrayList<>();
}
{
	(
		<NEWLINE>
		|
		{
			if(!BlockContinuation()){

				if(!statements.isEmpty()){
					return statements;
				}
			}
		}
		statement = Statement() { statements.addAll(statement); } 
	)+

	{
		return statements;
	}
}

private List<Expression> Statement():
{
	Expression statement;
	List<Expression> statements;
}
{
	{
		String indent = getIndent();

		checkIndent(indent);
	}

	(
		statement = CompoundStatement() { statements = Collections.singletonList(statement); }
		|
		statements = SimpleStatements()
	)

	{
		return statements;
	}
}

private Expression CompoundStatement():
{
	Expression statement;
}
{
	statement = IfStatement()

	{
		return statement;
	}
}

private List<Expression> SimpleStatements():
{
	Expression statement;
	List<Expression> statements = new ArrayList<>();
}
{
	statement = SimpleStatement() { statements.add(statement); } ( <SEMICOLON> statement = SimpleStatement() { statements.add(statement); } )* <NEWLINE>
	
	{
		return statements;
	}
}

private Expression SimpleStatement():
{
	Expression expression;
}
{
	(
		expression = Assignment()
		|
		expression = ReturnStatement()
		|
		expression = ImportStatement()
	)
	
	{
		return expression;
	}
}

private Apply IfStatement():
{
	Expression condition;
	Block block;

	Apply if_statement;
	Apply elif_statement;

	Apply tail;
}
{
	<IF> condition = Expression() <COLON> block = Block() { if_statement = ExpressionUtil.createApply(PMMLFunctions.IF, condition, block.getOnlyStatement()); tail = if_statement; } ( elif_statement = ElIfStatement() { tail.addExpressions(elif_statement); tail = elif_statement; } )* ( block = ElseBlock() { tail.addExpressions(block.getOnlyStatement()); } )?

	{
		return if_statement;
	}
}

private Apply ElIfStatement():
{
	Expression condition;
	Block block;
}
{
	<ELIF> condition = Expression() <COLON> block = Block()

	{
		return ExpressionUtil.createApply(PMMLFunctions.IF, condition, block.getOnlyStatement());
	}
}

private Block ElseBlock():
{
	Block block;
}
{
	<ELSE> <COLON> block = Block()

	{
		return block;
	}
}

private Expression Assignment():
{
	Token name;
	Expression expression;
}
{
	(
		LOOKAHEAD(<NAME> <ASSIGN>)
		name = <NAME> <ASSIGN>
		|
		{ name = null; }
	)
	expression = Expression()
	
	{
		if(name != null){
			putVariable(name.image, expression);
		}

		return null;
	}
}

private Expression ReturnStatement():
{
	Expression expression;
}
{
	<RETURN> expression = Expression()
	
	{
		return expression;
	}
}

private Expression ImportStatement():
{}
{
	<IMPORT> DottedAsName() ( <COMMA> DottedAsName() )*

	{
		return null;
	}
}

private void DottedAsName():
{
	String dottedName;
	Token name = null;
}
{
	dottedName = DottedName() ( <AS> name = <NAME> )?

	{
		Map<String, String> moduleImports = getModuleImports();

		moduleImports.put(name != null ? name.image : dottedName, dottedName);
	}
}
	
private Expression Expression():
{
	Expression expression;
}
{
	expression = IfElseExpression()

	{
		return expression;
	}
}

private Expression IfElseExpression():
{
	Expression left;
	Expression center;
	Expression right;
}
{
	left = LogicalOrExpression() ( <IF> center = Expression() <ELSE> right = Expression() { left = ExpressionUtil.createApply(PMMLFunctions.IF, center, left, right); } )?

	{
		return left;
	}
}

private Expression LogicalOrExpression():
{
	Expression left;
	Expression right;
}
{
	left = LogicalAndExpression() ( <OR> right = LogicalAndExpression() { left = ExpressionUtil.createApply(PMMLFunctions.OR, left, right); } )*

	{
		return left;
	}
}

private Expression LogicalAndExpression():
{
	Expression left;
	Expression right;
}
{
	left = NegationExpression() ( <AND> right = NegationExpression() { left = ExpressionUtil.createApply(PMMLFunctions.AND, left, right); } )*

	{
		return left;
	}
}

private Expression NegationExpression():
{
	Token operator = null;
	Expression expression;
}
{
	( operator = <NOT> )? expression = ComparisonExpression()

	{
		if(operator != null){
			return ExpressionUtil.createApply(PMMLFunctions.NOT, expression);
		}

		return expression;
	}
}

private Expression ComparisonExpression():
{
	Expression left;
	Token operator;
	Object right;
}
{
	left = AdditiveExpression()
	(
		LOOKAHEAD(<IS> <NONE>)
		<IS> <NONE> { left = ExpressionUtil.createApply(PMMLFunctions.ISMISSING, left); }
		|
		<IS> <NOT> <NONE> { left = ExpressionUtil.createApply(PMMLFunctions.ISNOTMISSING, left); }
		|
		<IN> right = ListMakerExpression() { left = ExpressionUtil.createApply(PMMLFunctions.ISIN, left).addExpressions((Expression[])right); }
		|
		<NOT> <IN> right = ListMakerExpression() { left = ExpressionUtil.createApply(PMMLFunctions.ISNOTIN, left).addExpressions((Expression[])right); }
		|
		((operator = <EQUAL> | operator = <NOT_EQUAL> | operator = <LESS_THAN> | operator = <LESS_OR_EQUAL> | operator = <GREATER_THAN> | operator = <GREATER_OR_EQUAL>) right = AdditiveExpression() { left = ExpressionUtil.createApply(translateRelationalOperator(operator), left, (Expression)right); } )?
	)

	{
		return left;
	}
}

private Expression AdditiveExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = MultiplicativeExpression() ((operator = <PLUS> | operator = <MINUS>) right = MultiplicativeExpression() {

		if((operator.kind == PLUS) && (ExpressionUtil.isString(left, this) && ExpressionUtil.isString(right, this))){
			left = ExpressionUtil.createApply(PMMLFunctions.CONCAT, left, right);
		} else

		{
			left = ExpressionUtil.createApply(translateArithmeticOperator(operator), left, right);
		}
	})*

	{
		return left;
	}
}

private Expression MultiplicativeExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = UnaryExpression() ((operator = <MULTIPLY> | operator = <DIVIDE> | operator = <MODULO> ) right = UnaryExpression() { left = ExpressionUtil.createApply(translateArithmeticOperator(operator), left, right); } )*

	{
		return left;
	}
}

private Expression UnaryExpression():
{
	Token sign = null;
	Expression expression;
}
{
	(
		sign = Sign() expression = UnaryExpression()
		|
		expression = PowerExpression()
	)

	{
		if(sign != null && sign.kind == MINUS){
			expression = ExpressionUtil.toNegative(expression);
		}

		return expression;
	}
}

private Expression PowerExpression():
{
	Expression left;
	Expression right;
}
{
	left = PrimaryExpression() ( <POWER> right = UnaryExpression() { left = ExpressionUtil.createApply(PMMLFunctions.POW, left, right); } )?

	{
		return left;
	}
}

private Expression PrimaryExpression():
{
	String dottedName;
	Expression expression;
}
{
	(
		dottedName = CanonicalizedDottedName()
		(
			LOOKAHEAD(ArrayIndexingExpression())
			expression = ArrayIndexingExpression(dottedName) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
			|
			expression = FunctionInvocationExpression(dottedName) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
			|
			expression = NameInvocationExpression(dottedName) ( expression = StringSlicingExpression(expression) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )* )?
		)
		|
		expression = ParenthesizedExpression() ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
		|
		expression = LiteralExpression()
	)

	{
		return expression;
	}
}

private FieldRef ArrayIndexingExpression(String dottedName):
{
	Scope scope = ensureScope();

	int colIndex;
	String colName;

	Feature feature;
}
{
	(
		LOOKAHEAD(<LBRACKET> ArrayColumnIndex())
		<LBRACKET> colIndex = ArrayColumnIndex() <RBRACKET> {
			feature = scope.getFeature(dottedName, colIndex);
		}
		|
		<LBRACKET> colName = ColumnName() <RBRACKET> {
			feature = scope.getFeature(dottedName, colName);
		}
	)

	{
		return feature.ref();
	}
}

private Expression FunctionInvocationExpression(String dottedName):
{
	Scope scope = ensureScope();

	List<Expression> arguments;
}
{
	arguments = Arguments()

	{
		int dot = dottedName.lastIndexOf('.');
		if(dot > -1){
			String featureName = dottedName.substring(0, dot);
			String functionName = dottedName.substring(dot + 1);

			Feature feature = scope.resolveFeature(featureName);
			if(feature != null){
				return translateTrailerFunction(feature.ref(), functionName, arguments);
			}
		}

		return encodeFunction(dottedName, arguments);
	}
}

private Apply TrailerFunctionInvocationExpression(Expression expression):
{
	Token name;
	List<Expression> arguments;
}
{
	<DOT> name = <NAME> arguments = Arguments()

	{
		return translateTrailerFunction(expression, name.image, arguments);
	}
}

private List<Expression> Arguments():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	(
		LOOKAHEAD(<LPAREN> <RPAREN>)
		<LPAREN> <RPAREN>
		|
		<LPAREN> expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); } )* <RPAREN>
	)

	{
		return expressions;
	}
}

private Expression StringSlicingExpression(Expression expression):
{
	Integer startPos = null;
	Integer stopPos = null;
}
{
	<LBRACKET> (startPos = ColumnIndex())? <COLON> (stopPos = ColumnIndex())? <RBRACKET>

	{
		return translateStringSlice(expression, startPos, stopPos);
	}
}

private Expression NameInvocationExpression(String dottedName):
{
	Scope scope = ensureScope();
}
{
	{
		int dot = dottedName.indexOf('.');
		if(dot > -1){
			return encodeConstant(dottedName);
		}

		FieldRef fieldRef = getVariable(dottedName);
		if(fieldRef != null){
			return fieldRef;
		}

		Feature feature = scope.getFeature(dottedName);

		return feature.ref();
	}
}

private Expression ParenthesizedExpression():
{
	Expression expression;
}
{
	<LPAREN> expression = Expression() <RPAREN>

	{
		return expression;
	}
}

private Constant LiteralExpression():
{
	Token value;
	DataType dataType;
	Boolean missing = null;
}
{
	(
		value = <NONE> { dataType = null; missing = Boolean.TRUE; }
		|
		value = <FALSE> { dataType = DataType.BOOLEAN; }
		|
		value = <TRUE> { dataType = DataType.BOOLEAN; }
		|
		value = <INT> { dataType = DataType.INTEGER; }
		|
		value = <FLOAT> { dataType = DataType.DOUBLE; }
		|
		value = String() { dataType = DataType.STRING; }
	)

	{
		Constant constant = new Constant()
			.setValue(PythonParserUtil.parseValue(value))
			.setDataType(dataType)
			.setMissing(missing);

		return constant;
	}
}

private Expression[] ListMakerExpression():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	<LBRACKET> expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); } )* <RBRACKET>

	{
		return expressions.toArray(new Expression[expressions.size()]);
	}
}
