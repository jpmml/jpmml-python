/*
 * Copyright (c) 2017 Villu Ruusmann
 *
 * This file is part of JPMML-Python
 *
 * JPMML-Python is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JPMML-Python is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with JPMML-Python.  If not, see <http://www.gnu.org/licenses/>.
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "1.8";
	STATIC = false;
}

PARSER_BEGIN(ExpressionTranslator)
package org.jpmml.python;

import java.util.ArrayList;
import java.util.List;

import org.dmg.pmml.Constant;
import org.dmg.pmml.DataType;
import org.dmg.pmml.Expression;
import org.dmg.pmml.FieldRef;
import org.dmg.pmml.PMMLFunctions;
import org.jpmml.converter.Feature;
import org.jpmml.converter.PMMLUtil;
import org.jpmml.converter.TypeUtil;
import org.jpmml.converter.visitors.ExpressionCompactor;

public class ExpressionTranslator extends AbstractTranslator {

	static
	public Expression translate(String string, Scope scope){
		return translate(string, scope, true);
	}

	static
	public Expression translate(String string, Scope scope, boolean compact){
		Expression expression;

		try {
			ExpressionTranslator expressionTranslator = new ExpressionTranslator(string);
			expressionTranslator.setScope(scope);

			expression = expressionTranslator.translateExpressionInternal();
		} catch(ParseException pe){
			throw new IllegalArgumentException("Python expression \'" + string + "\' is either invalid or not supported", pe);
		}

		if(compact){
			ExpressionCompactor compactor = new ExpressionCompactor();

			compactor.applyTo(expression);
		}

		return expression;
	}

	static
	private String translateRelationalOperator(Token operator){

		switch(operator.kind){
			case EQUAL:
				return PMMLFunctions.EQUAL;
			case NOT_EQUAL:
				return PMMLFunctions.NOTEQUAL;
			case LESS_THAN:
				return PMMLFunctions.LESSTHAN;
			case LESS_OR_EQUAL:
				return PMMLFunctions.LESSOREQUAL;
			case GREATER_THAN:
				return PMMLFunctions.GREATERTHAN;
			case GREATER_OR_EQUAL:
				return PMMLFunctions.GREATEROREQUAL;
			default:
				throw new IllegalArgumentException(operator.image);
		}
	}

	static
	private String translateArithmeticOperator(Token operator){

		switch(operator.kind){
			case MODULO:
				return PMMLFunctions.MODULO;
			default:
				return operator.image;
		}
	}

	static
	private Constant translateConstant(String module, Token value){
		
		if(("numpy").equals(module) || ("np").equals(module)){
			
			switch(value.kind){
				case NAN:
					return PMMLUtil.createMissingConstant();
				default:
					throw new IllegalArgumentException();
			}
		}
		
		throw new IllegalArgumentException(module);
	}

	static
	private Expression translateTrailerFunction(Expression expression, String function, List<Expression> arguments){

		if(("startswith").equals(function) || ("endswith").equals(function)){

			if(arguments.size() != 1){
				throw new IllegalArgumentException();
			}

			Expression argument = arguments.get(0);
			if(!(argument instanceof Constant)){
				throw new IllegalArgumentException();
			}

			Constant constant = (Constant)argument;
			if(constant.getDataType() != DataType.STRING){
				throw new IllegalArgumentException();
			}

			String value = (String)constant.getValue();

			switch(function){
				case "startswith":
					return PMMLUtil.createApply(PMMLFunctions.EQUAL,
						translateStringSlice(expression, 0, value.length()),
						constant
					);
				case "endswith":
					return PMMLUtil.createApply(PMMLFunctions.EQUAL,
						translateStringSlice(expression, -value.length(), null),
						constant
					);
				default:
					break;
			}
		} else

		if(("lower").equals(function) || ("upper").equals(function)){

			if(arguments.size() != 0){
				throw new IllegalArgumentException();
			}

			switch(function){
				case "lower":
					return PMMLUtil.createApply(PMMLFunctions.LOWERCASE, expression);
				case "upper":
					return PMMLUtil.createApply(PMMLFunctions.UPPERCASE, expression);
				default:
					break;
			}
		} else

		if(("strip").equals(function)){

			if(arguments.size() != 0){
				throw new IllegalArgumentException();
			}

			return PMMLUtil.createApply(PMMLFunctions.TRIMBLANKS, expression);
		}

		throw new IllegalArgumentException(function);
	}

	static
	private Expression translateFunction(String function, List<Expression> arguments){
		String module;
		String name;

		int dot = function.lastIndexOf('.');
		if(dot > -1){
			module = function.substring(0, dot);
			name = function.substring(dot + 1);
		} else

		{
			module = "";
			name = function;
		}

		if(("").equals(module)){
			return FunctionUtil.encodeFunction("builtins", name, arguments);
		} else

		if(("numpy").equals(module) || ("np").equals(module)){
			return FunctionUtil.encodeFunction("numpy", name, arguments);
		} else

		if(("pandas").equals(module) || ("pd").equals(module)){
			return FunctionUtil.encodeFunction("pandas", name, arguments);
		}

		throw new IllegalArgumentException(function);
	}

	static
	private Expression translateStringSlice(Expression expression, Integer startPos, Integer stopPos){

		if(startPos == null && stopPos == null){
			return expression;
		}

		Expression offsetExpression;

		if(startPos != null && startPos >= 0){
			offsetExpression = PMMLUtil.createConstant(startPos + 1, DataType.INTEGER);
		} else

		if(startPos != null && startPos < 0){
			offsetExpression = PMMLUtil.createApply(PMMLFunctions.MAX,
				PMMLUtil.createApply(PMMLFunctions.SUBTRACT, 
					PMMLUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
					PMMLUtil.createConstant((-startPos) - 1, DataType.INTEGER)
				),
				PMMLUtil.createConstant(1, DataType.INTEGER)
			);
		} else

		{
			offsetExpression = PMMLUtil.createConstant(1, DataType.INTEGER);
		} // End if

		Expression lengthExpression;

		if(stopPos != null && stopPos >= 0){

			if(startPos != null && startPos >= 0){
				lengthExpression = PMMLUtil.createConstant(Math.max(stopPos - startPos, 0), DataType.INTEGER);
			} else

			if(startPos != null && startPos < 0){
				lengthExpression = PMMLUtil.createConstant(0, DataType.INTEGER);
			} else

			{
				lengthExpression = PMMLUtil.createConstant(stopPos, DataType.INTEGER);
			}
		} else

		if(stopPos != null && stopPos < 0){

			if(startPos != null && startPos >= 0){
				lengthExpression = PMMLUtil.createApply(PMMLFunctions.MAX,
					PMMLUtil.createApply(PMMLFunctions.SUBTRACT,
						PMMLUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
						PMMLUtil.createConstant(startPos + (-stopPos))
					),
					PMMLUtil.createConstant(0, DataType.INTEGER)
				);
			} else

			if(startPos != null && startPos < 0){
				lengthExpression = PMMLUtil.createConstant(Math.max(stopPos - startPos, 0), DataType.INTEGER);
			} else

			{
				lengthExpression = PMMLUtil.createApply(PMMLFunctions.MAX,
					PMMLUtil.createApply(PMMLFunctions.SUBTRACT,
						PMMLUtil.createApply(PMMLFunctions.STRINGLENGTH, expression),
						PMMLUtil.createConstant(-stopPos)
					),
					PMMLUtil.createConstant(0, DataType.INTEGER)
				);
			}
		} else

		{
			lengthExpression = PMMLUtil.createConstant(Integer.MAX_VALUE, DataType.INTEGER);
		}

		return PMMLUtil.createApply(PMMLFunctions.SUBSTRING, expression, offsetExpression, lengthExpression);
	}

	static
	private Object translateValue(Token value){
		String image = value.image;

		switch(value.kind){
			case NONE:
				return null;
			case FALSE:
			case TRUE:
				return Boolean.valueOf(image.toLowerCase());
			case INT:
				if(image.endsWith("l") || image.endsWith("L")){
					image = image.substring(0, image.length() - 1);
				}
				return Integer.valueOf(image);
			case FLOAT:
				return Double.valueOf(image);
			case STRING:
				return image.substring(1, image.length() - 1);
			default:
				throw new IllegalArgumentException();
		}
	}

	static
	private int parseInt(Token sign, Token value){
		return Integer.parseInt(sign != null ? (sign.image + value.image) : value.image);
	}
}
PARSER_END(ExpressionTranslator)

SPECIAL_TOKEN:
{
	" "
	|
	"\t"
}

TOKEN:
{
	< LPAREN: "(" >
	|
	< RPAREN: ")" >
}

TOKEN:
{
	< IF: "if" >
	|
	< ELSE: "else" >
	|
	< OR: "or" >
	|
	< AND: "and" >
	|
	< NOT: "not" >
}

TOKEN:
{
	< IS: "is" >
	|
	< IN: "in" >
	|
	< EQUAL: "==" >
	|
	< NOT_EQUAL: "!=" >
	|
	< LESS_THAN: "<" >
	|
	< LESS_OR_EQUAL: "<=" >
	|
	< GREATER_THAN: ">" >
	|
	< GREATER_OR_EQUAL: ">=" >
}

TOKEN:
{
	< PLUS: "+" >
	|
	< MINUS: "-" >
	|
	< MULTIPLY: "*" >
	|
	< DIVIDE: "/" >
	|
	< MODULO: "%" >
	|
	< TILDE: "~" >
}

TOKEN:
{
	< LBRACKET: "[" >
	|
	< RBRACKET: "]" >
}

TOKEN:
{
	< COMMA: "," >
	|
	< DOT: "." >
	|
	< COLON: ":" >
}

TOKEN:
{
	< NONE: "None" >
}

TOKEN:
{
	< FALSE: "False" >
	|
	< TRUE: "True" >
}

TOKEN:
{
	< NAN:
		"nan"
		|
		"NaN"
		|
		"NAN"
	>
}

TOKEN:
{
	< INT:
		"0"
		|
		<NONZERO_DIGIT> (<DIGIT>)* ( ["l", "L"] )?
	>
	|
	< FLOAT:
		(<DIGIT>)+ "." (<DIGIT>)* (<EXPONENT>)?
		|
		"." (<DIGIT>)+ (<EXPONENT>)?
		|
		(<DIGIT>)+ <EXPONENT>
	>
	|
	< NAME: <LETTER> (<LETTER> | <DIGIT>)* >
	|
	< STRING:
		"\"" (~["\""])* "\""
		|
		"\'" (~["\'"])* "\'"
	>
	|
	< #DIGIT: ["0" - "9"] >
	|
	< #NONZERO_DIGIT: ["1" - "9"] >
	|
	< #LETTER: ["a" - "z", "A" - "Z", "_"]>
	|
	< #EXPONENT: ["e", "E"] (["+", "-"])? (<DIGIT>)+ >
}

private Expression translateExpressionInternal():
{
	Expression expression;
}
{
	expression = Expression() <EOF>

	{
		return expression;
	}
}

Expression Expression():
{
	Expression expression;
}
{
	expression = IfElseExpression()

	{
		return expression;
	}
}

Expression IfElseExpression():
{
	Expression left;
	Expression center;
	Expression right;
}
{
	left = LogicalOrExpression() ( <IF> center = Expression() <ELSE> right = Expression() { left = PMMLUtil.createApply(PMMLFunctions.IF, center, left, right); } )?

	{
		return left;
	}
}

Expression LogicalOrExpression():
{
	Expression left;
	Expression right;
}
{
	left = LogicalAndExpression() ( <OR> right = LogicalAndExpression() { left = PMMLUtil.createApply(PMMLFunctions.OR, left, right); } )*

	{
		return left;
	}
}

Expression LogicalAndExpression():
{
	Expression left;
	Expression right;
}
{
	left = NegationExpression() ( <AND> right = NegationExpression() { left = PMMLUtil.createApply(PMMLFunctions.AND, left, right); } )*

	{
		return left;
	}
}

Expression NegationExpression():
{
	Token operator = null;
	Expression expression;
}
{
	( operator = <NOT> )? expression = ComparisonExpression()

	{
		if(operator != null){
			return PMMLUtil.createApply(PMMLFunctions.NOT, expression);
		}

		return expression;
	}
}

Expression ComparisonExpression():
{
	Expression left;
	Token operator;
	Object right;
}
{
	(
		LOOKAHEAD(AdditiveExpression() <IS> <NONE>)
		left = AdditiveExpression() <IS> <NONE> { left = PMMLUtil.createApply(PMMLFunctions.ISMISSING, left); }
		|
		LOOKAHEAD(AdditiveExpression() <IS> <NOT> <NONE>)
		left = AdditiveExpression() <IS> <NOT> <NONE> { left = PMMLUtil.createApply(PMMLFunctions.ISNOTMISSING, left); }
		|
		LOOKAHEAD(AdditiveExpression() <IN>)
		left = AdditiveExpression() <IN> right = ListMakerExpression() { left = PMMLUtil.createApply(PMMLFunctions.ISIN, left).addExpressions((Expression[])right); }
		|
		LOOKAHEAD(AdditiveExpression() <NOT> <IN>)
		left = AdditiveExpression() <NOT> <IN> right = ListMakerExpression() { left = PMMLUtil.createApply(PMMLFunctions.ISNOTIN, left).addExpressions((Expression[])right); }
		|
		left = AdditiveExpression() ((operator = <EQUAL> | operator = <NOT_EQUAL> | operator = <LESS_THAN> | operator = <LESS_OR_EQUAL> | operator = <GREATER_THAN> | operator = <GREATER_OR_EQUAL>) right = AdditiveExpression() { left = PMMLUtil.createApply(translateRelationalOperator(operator), left, (Expression)right); })?
	)

	{
		return left;
	}
}

Expression AdditiveExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = MultiplicativeExpression() ((operator = <PLUS> | operator = <MINUS>) right = MultiplicativeExpression() {
		Scope scope = ensureScope();

		if((operator.kind == PLUS) && (TypeUtil.isString(left, scope) && TypeUtil.isString(right, scope))){
			left = PMMLUtil.createApply(PMMLFunctions.CONCAT, left, right);
		} else

		{
			left = PMMLUtil.createApply(translateArithmeticOperator(operator), left, right);
		}
	})*

	{
		return left;
	}
}

Expression MultiplicativeExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = UnaryExpression() ((operator = <MULTIPLY> | operator = <DIVIDE> | operator = <MODULO> ) right = UnaryExpression() { left = PMMLUtil.createApply(translateArithmeticOperator(operator), left, right); })*

	{
		return left;
	}
}

Expression UnaryExpression():
{
	Token sign = null;
	Expression expression;
}
{
	(
		sign = <PLUS> expression = UnaryExpression()
		|
		sign = <MINUS> expression = UnaryExpression()
		|
		expression = PrimaryExpression()
	)

	{
		if(sign != null && sign.kind == MINUS){
			expression = PMMLUtil.toNegative(expression);
		}

		return expression;
	}
}

Expression PrimaryExpression():
{
	String featureOrFunction;
	Expression expression;
}
{
	(
		featureOrFunction = DottedName()
		(
			LOOKAHEAD(ArrayIndexingExpression())
			expression = ArrayIndexingExpression(featureOrFunction) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
			|
			expression = FunctionInvocationExpression(featureOrFunction) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
			|
			expression = ConstantExpression(featureOrFunction)
			|
			expression = NameInvocationExpression(featureOrFunction) ( expression = StringSlicingExpression(expression) ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )* )?
		)
		|
		expression = ParenthesizedExpression() ( expression = TrailerFunctionInvocationExpression(expression) | expression = StringSlicingExpression(expression) )*
		|
		expression = LiteralExpression()
	)

	{
		return expression;
	}
}

String DottedName():
{
	Token name;
	StringBuilder sb = new StringBuilder();
}
{
	name = <NAME> { sb.append(name.image); } ( LOOKAHEAD(<DOT> <NAME>) <DOT> name = <NAME> { sb.append('.').append(name.image); })*

	{
		return sb.toString();
	}
}

FieldRef ArrayIndexingExpression(String dottedName):
{
	Token sign = null;
	Token column;

	Feature feature;
}
{
	(
		LOOKAHEAD(<LBRACKET> (Sign())? <INT>)
		<LBRACKET> (sign = Sign())? column = <INT> <RBRACKET> {
			Scope scope = ensureScope();
			
			int colIndex = parseInt(sign, column);
			
			feature = scope.getFeature(dottedName, colIndex);
		}
		|
		<LBRACKET> column = <STRING> <RBRACKET> {
			Scope scope = ensureScope();
			
			String colName = column.image.substring(1, column.image.length() - 1);
			
			feature = scope.getFeature(dottedName, colName);
		}
	)

	{
		return feature.ref();
	}
}

Token Sign():
{
	Token token;
}
{
	(
		token = <PLUS> 
		|
		token = <MINUS>
	)

	{
		return token;
	}
}

Expression FunctionInvocationExpression(String dottedName):
{
	List<Expression> arguments;
}
{
	arguments = Arguments()

	{
		Scope scope = ensureScope();

		int dot = dottedName.lastIndexOf('.');
		if(dot > -1){
			String featureName = dottedName.substring(0, dot);
			String functionName = dottedName.substring(dot + 1);

			Feature feature = scope.resolveFeature(featureName);
			if(feature != null){
				return translateTrailerFunction(feature.ref(), functionName, arguments);
			}
		}

		return translateFunction(dottedName, arguments);
	}
}

Expression TrailerFunctionInvocationExpression(Expression expression):
{
	Token name;
	List<Expression> arguments;
}
{
	<DOT> name = <NAME> arguments = Arguments()

	{
		return translateTrailerFunction(expression, name.image, arguments);
	}
}

List<Expression> Arguments():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	(
		LOOKAHEAD(<LPAREN> <RPAREN>)
		<LPAREN> <RPAREN>
		|
		<LPAREN> expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); })* <RPAREN>
	)

	{
		return expressions;
	}
}

Expression StringSlicingExpression(Expression expression):
{
	Token startSign = null;
	Token start = null;
	Token stopSign = null;
	Token stop = null;
}
{
	<LBRACKET> ((startSign = Sign())? start = <INT>)? <COLON> ((stopSign = Sign())? stop = <INT>)? <RBRACKET>

	{
		Integer startPos = null;
		if(start != null){
			startPos = parseInt(startSign, start);
		}

		Integer stopPos = null;
		if(stop != null){
			stopPos = parseInt(stopSign, stop);
		}

		return translateStringSlice(expression, startPos, stopPos);
	}
}

Constant ConstantExpression(String dottedName):
{
	Token value;
}
{
	<DOT> value = <NAN>
	
	{
		return translateConstant(dottedName, value);
	}
}

FieldRef NameInvocationExpression(String dottedName):
{
}
{
	{
		Scope scope = ensureScope();

		Feature feature = scope.getFeature(dottedName);

		return feature.ref();
	}
}

Expression ParenthesizedExpression():
{
	Expression expression;
}
{
	<LPAREN> expression = Expression() <RPAREN>

	{
		return expression;
	}
}

Constant LiteralExpression():
{
	Token value;
	DataType dataType;
	Boolean missing = null;
}
{
	(
		value = <NONE> { dataType = null; missing = Boolean.TRUE; }
		|
		value = <FALSE> { dataType = DataType.BOOLEAN; }
		|
		value = <TRUE> { dataType = DataType.BOOLEAN; }
		|
		value = <INT> { dataType = DataType.INTEGER; }
		|
		value = <FLOAT> { dataType = DataType.DOUBLE; }
		|
		value = <STRING> { dataType = DataType.STRING; }
	)

	{
		Constant constant = new Constant()
			.setValue(translateValue(value))
			.setDataType(dataType)
			.setMissing(missing);

		return constant;
	}
}

Expression[] ListMakerExpression():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	<LBRACKET> expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); })* <RBRACKET>

	{
		return expressions.toArray(new Expression[expressions.size()]);
	}
}
